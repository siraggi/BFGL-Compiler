dcl num Height
dcl num Wight

Main begin
    dcl Player player to new Player()
    dcl Enemy enemy to new Enemy()
    set Height to game.height
    set Wight to game.width

end

class Player is Sprite begin
    dcl num speed
    dcl num vel

    OnConstruct() do
        base(40, Height/2, 20, 150, "Player")
        set speed to 1
        game.Sprites.add(this)
    end

    OnUpdate() do
        if input.key.W.isDown then
             set vel to -speed
        else if input.key.S.isDown then
             set vel to speed
        else
            set vel to 0
        end
        set posY to posY+vel
    end
end

class Enemy is Sprite begin
  dcl num speed to 1
  dcl num vel
  dcl Ball ball
  
  OnConstruct() do
    base(Wight - 40, 100, 20, 150, "Enemy")
    game.Sprites.add(this)
    set ball to new Ball()
  end

  OnUpdate() do                           /* A event as explained in the previous section       */
    if posY < ball.posY then                /* Use the reference obtained earlier, to move the enemy accordingly */
      set vel to speed

    else if posY > ball.posY then
      set vel to -speed
    end
    set posY to posY+vel
  end
end

class Ball is Sprite begin
  dcl num speed
  dcl Vector vect

  OnConstruct() do
    base(200, 200, 20,20, "Ball")
    set vect to new Vector(-1,0)
    set speed to 0.2
    game.Sprites.add(this)
  end

  OnCollision(Sprite other) do
    set vect to GetCenter()
    set vect to vect.DirVector(other.GetCenter())
    vect.Normelise()
    set speed to 0.01
  end

  OnUpdate() do
    if posY < 0 or posY > Height then
      vect.SetY(-vect.y)
    end
    set posX to posX+vect.x*speed
    set posY to posY+vect.y*speed
  end
end